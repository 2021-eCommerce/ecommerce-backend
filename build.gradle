buildscript {
    ext {
        springVersion = "2.4.2"
        queryDslVersion = "5.0.0"
        jwtVersion = "0.11.5"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'

    //querydsl 추가
    id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'

    // rest doc
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'store.juin'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    // rest doc
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    mapstructVersion = "1.4.2.Final"
    lombokVersion = "1.18.20"
    lombokMapstructBindingVersion = "0.2.0"

    // rest doc
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    // swagger
    implementation 'io.springfox:springfox-boot-starter:3.0.0'

    // jpa
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springVersion}"

    // web
    implementation "org.springframework.boot:spring-boot-starter-web:${springVersion}"

    // security
    implementation "org.springframework.boot:spring-boot-starter-security:${springVersion}"
    implementation 'org.springframework.session:spring-session:1.3.2.RELEASE'

    // MySQL
    implementation "org.springframework.boot:spring-boot-starter-data-jdbc:${springVersion}"
    runtimeOnly 'mysql:mysql-connector-java'

    // lombok
    compile "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // JWT
    implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
    implementation "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
    implementation "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"

    //querydsl 추가
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
    implementation "com.querydsl:querydsl-apt:${queryDslVersion}"
    implementation "com.querydsl:querydsl-core:${queryDslVersion}"

    // p6spy logging
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.1'

    // JUnit
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    // mockito
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-inline:4.8.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.12.10' // java 17 mock 오류 발생해서 추가
    testImplementation 'org.springframework.security:spring-security-test'

    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // s3
    implementation group: 'io.awspring.cloud', name: 'spring-cloud-starter-aws', version: '2.4.2'

    // rest doc
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // MariaDB
//	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // redis
//	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
//    implementation 'org.springframework.session:spring-session:1.3.5.RELEASE'

    // DTO Mapper
//    implementation "org.mapstruct:mapstruct:${mapstructVersion}", "org.projectlombok:lombok:${lombokVersion}"
//    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}", "org.projectlombok:lombok:${lombokVersion}", "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    // thymeleaf
//    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
//    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir // rest doc
}

asciidoctor.doFirst {
    //asciidoctor 실행전 기존에 생성된 API 문서 삭제
    delete file('BOOT-INF/classes/static/docs')
    delete file('src/main/resources/static/docs')
}

// rest doc
asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

// rest doc
bootJar {
    duplicatesStrategy = DuplicatesStrategy.INHERIT

    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into "BOOT-INF/classes/static/docs"
    }
}


// rest doc
tasks.register('copyDocument', Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

// rest doc
build {
    dependsOn copyDocument
}

//querydsl 추가 시작
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main.java.srcDir querydslDir
}
compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}
//querydsl 추가 끝